-- This is basically my old Languishing Email script, revamped to work with macOS's Mail app.

set unrepliedMinutes to 20
set unreadMinutes to 10

set numUnread to 0
set numUnreplied to 0

tell application "System Events"
	set appName to item 1 of (get name of processes whose frontmost is true)
end tell

if (appName is not "Mail") then
	tell application "Mail"
		set unrepliedMaxAge to (60 * unrepliedMinutes)
		set unreadMaxAge to (60 * unreadMinutes)
		set now to (current date)
		log "unrepliedMaxAge: " & (unrepliedMaxAge)
		log "now: " & (now as string)
		set inboxes to first mailbox of every account whose name is "Inbox"
		repeat with i from 1 to number of items in inboxes
			set this_item to item i of inboxes
			if this_item is not missing value then
				repeat with m in messages of this_item
					set sentTime to (m's date received as date)
					set a to now - sentTime
					log (m's subject as string)
					log "read   : " & (m's read status as string)
					log "replied: " & (m's was replied to as string)
					log "sent: " & (sentTime as string)
					log "age: " & a
					if m's read status is false and a > unreadMaxAge then
						log "    UNREAD"
						set numUnread to (numUnread + 1)
					else if m's was replied to is false and a > unrepliedMaxAge then
						log "UNREPLIED"
						set numUnreplied to (numUnreplied + 1)
					else
						log "NO PROBLEM"
					end if
				end repeat
			end if
		end repeat
	end tell
	
	if numUnread > 0 or numUnreplied > 0 then
		tell application "Finder"
			activate
			set q to display dialog ¬
				"Languishing email: " & numUnread & " unread, " & numUnreplied & " unreplied." buttons {"Ignore", "Read now"} default button 1
			set a to button returned of q
		end tell
		if a is equal to "Read now" then
			tell application "Mail"
				activate
			end tell
		else
			tell application appName
				activate
			end tell
		end if
	end if
end if
